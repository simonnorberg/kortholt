buildscript {
    ext.versions = [
            'kotlin'     : '1.6.21',
            'compile_sdk': 32,
            'min_sdk'    : 28,
            'target_sdk' : 32,
            'ndk'        : '24.0.8215888',
            'activity'   : '1.5.0-rc01',
            'annotation' : '1.3.0',
            'compose'    : '1.2.0-beta03',
            'core'       : '1.8.0',
            'lifecycle'  : '2.5.0-rc01',
            'media'      : '1.6.0'
    ]
}

plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'com.android.library' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.21' apply false
    id 'com.diffplug.spotless' version '6.6.1'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

subprojects {
    configurations.configureEach {
        resolutionStrategy {
            eachDependency { details ->
                def group = details.requested.group
                if (group.startsWith('androidx.compose')) {
                    details.useVersion versions.compose
                }
                if (group == 'androidx.lifecycle') {
                    details.useVersion versions.lifecycle
                }
            }
        }
    }
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = '11'
            allWarningsAsErrors = true
        }
    }
    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target 'src/*/java/**/*.kt'
            ktlint('0.45.2')
        }
    }
}

Object propOrDef(String propertyName, Object defaultValue) {
    def propertyValue = project.properties[propertyName]
    return propertyValue != null ? propertyValue : defaultValue
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = propOrDef("SONATYPE_STAGING_PROFILE_ID", "")
            username = propOrDef("OSSRH_USERNAME", "")
            password = propOrDef("OSSRH_PASSWORD", "")
        }
    }
}
